apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Thepeer official Android SDK'
                    url = 'https://github.com/thepeerstack/thepeer-android'
                    licenses {
                        license {
                            name = 'Thepeer License'
                            url = ''
                        }
                    }
                    developers {
                        developer {
                            id = 'zsmb13'
                            name = 'MÃ¡rton Braun'
                            email = 'marton@getstream.io'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/thepeerstack/thepeer-android.git'
                        developerConnection = 'scm:git:ssh://github.com/thepeerstack/thepeer-android.git'
                        url = 'https://github.com/thepeerstack/thepeer-android/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signingKeyId"],
            rootProject.ext["signingKey"],
            rootProject.ext["signingPassword"],
    )
    sign publishing.publications
}